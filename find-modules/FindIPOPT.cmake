# Created:
# IPOPT_INCLUDE_DIRS      - Directories to include to use IPOPT
# IPOPT_LIBRARIES         - Default library to link against to use IPOPT
# IPOPT_LIBRARIES_RELEASE - Release library (for win) to link against to use IPOPT
# IPOPT_LIBRARIES_DEBUG   - Debug library (for win) to link against to use IPOPT
# IPOPT_FOUND             - If false, don't try to use IPOPT

SET(IPOPT_DIR $ENV{IPOPT_DIR} CACHE PATH "Path to IPOPT main directory")
SET(IPOPT_INCLUDE_DIRS ${IPOPT_DIR}/include/coin)

IF(WIN32)

   SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} /NODEFAULTLIB:libcmt.lib;libcmtd.lib")

   FIND_LIBRARY(IPOPT_LIBRARIES_RELEASE libipopt  ${IPOPT_DIR}/lib NO_DEFAULT_PATH)
   FIND_LIBRARY(IPOPT_LIBRARIES_DEBUG   libipoptD ${IPOPT_DIR}/lib NO_DEFAULT_PATH)
   IF(IPOPT_LIBRARIES_RELEASE AND IPOPT_LIBRARIES_DEBUG)
      SET(IPOPT_LIBRARIES optimized ${IPOPT_LIBRARIES_RELEASE} debug ${IPOPT_LIBRARIES_DEBUG})
   ENDIF(IPOPT_LIBRARIES_RELEASE AND IPOPT_LIBRARIES_DEBUG)

ELSE(WIN32)

   FIND_LIBRARY(IPOPT_LIBRARIES ipopt ${IPOPT_DIR}/lib NO_DEFAULT_PATH)
   IF(IPOPT_LIBRARIES)
      FILE(READ ${IPOPT_DIR}/share/doc/coin/Ipopt/ipopt_addlibs_cpp.txt IPOPT_DEP)
      STRING(REGEX REPLACE "-[^l][^ ]* " "" IPOPT_DEP ${IPOPT_DEP})
      STRING(REPLACE "-l" "" IPOPT_DEP ${IPOPT_DEP})
      STRING(REPLACE "\n" "" IPOPT_DEP ${IPOPT_DEP})
      SEPARATE_ARGUMENTS(IPOPT_DEP)
      SET(IPOPT_LIBRARIES         ${IPOPT_LIBRARIES} ${IPOPT_DEP})
      SET(IPOPT_LIBRARIES_RELEASE ${IPOPT_LIBRARIES})
      SET(IPOPT_LIBRARIES_DEBUG   ${IPOPT_LIBRARIES})
   ENDIF(IPOPT_LIBRARIES)

ENDIF(WIN32)

IF(IPOPT_LIBRARIES)
   SET(IPOPT_FOUND TRUE)
ELSE(IPOPT_LIBRARIES)
   SET(IPOPT_FOUND FALSE)
   UNSET(IPOPT_INCLUDE_DIRS)
   UNSET(IPOPT_LIBRARIES)
   UNSET(IPOPT_LIBRARIES_RELEASE)
   UNSET(IPOPT_LIBRARIES_DEBUG)
ENDIF(IPOPT_LIBRARIES)


